## zilsh_load_bundle(directory)
# Loads and executes a bundle in $directory

# TODO: switch to the zsh directory stack
# Keep the old BUNDLE_DIR so that we can restore it later, allowing
# bundles to be loaded recursively
local old_bundle_dir=$BUNDLE_DIR
BUNDLE_DIR=$1

zilsh_debug "Loading bundle from $BUNDLE_DIR"
timer=$(zilsh_bench_start)

if [[ -f "$BUNDLE_DIR/guard.zsh" ]] && ! zsh "$BUNDLE_DIR/guard.zsh"; then
	zilsh_debug "  Guardfile exited nonzero, aborting load."
	return 1
fi

# Log debug messages for missing directories and files
[[ -f "$BUNDLE_DIR/guard.zsh" ]]       || zilsh_debug "  No guard file found."
[[ -d "$BUNDLE_DIR/functions" ]]       || zilsh_debug "  No functions directory found."
[[ -d "$BUNDLE_DIR/themes" ]]          || zilsh_debug "  No themes directory found."
[[ -f "$BUNDLE_DIR/aliases.zsh" ]]     || zilsh_debug "  No aliases file found."
[[ -f "$BUNDLE_DIR/keybindings.zsh" ]] || zilsh_debug "  No keybindings file found."
[[ -f "$BUNDLE_DIR/init.zsh" ]]        || zilsh_debug "  No init file found."

# Add themes to $zsh_themes array
if [[ -d "$BUNDLE_DIR/themes" ]]; then
	for theme_file ($BUNDLE_DIR/themes/*.zsh-theme); do
		zsh_themes[${theme_file:t:r}]=${theme_file:a}
	done
fi

# Add functions to the fpath
if [[ -d "$BUNDLE_DIR/functions" ]]; then
	fpath=($BUNDLE_DIR/functions(:a) $fpath)
	autoload $BUNDLE_DIR/functions/*(:t)
	zilsh_debug "  Functions loaded."
fi

# Load aliases
# This is temporary, eventually the plan is to use `aliases/*.zsh-alias` instead.  But that takes
# a bit more work to get right, this is more loose and can easily be switched to that without
# breaking anything.
[[ -f "$BUNDLE_DIR/aliases.zsh" ]] && source "$BUNDLE_DIR/aliases.zsh" && zilsh_debug "  Aliases loaded."

# load keybindings
[[ -f "$BUNDLE_DIR/keybindings.zsh" ]] && source "$BUNDLE_DIR/keybindings.zsh" && zilsh_debug "  Keybindings loaded."

# Source the init.zsh file
[[ -f "$BUNDLE_DIR/init.zsh" ]] && source "$BUNDLE_DIR/init.zsh" && zilsh_debug "Bundle in $BUNDLE_DIR initialized."

zilsh_debug "Done loading $BUNDLE_DIR (Took $(zilsh_bench_end $timer))"
BUNDLE_DIR=$old_bundle_dir

